plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

group = 'com.ronja.crm'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}"

ext {
    testcontainersVersion = '1.15.3'
    jacocoDestinationFile = file("$buildDir/jacoco/test.exec")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'mysql:mysql-connector-java'
    implementation 'org.flywaydb:flyway-core:7.10.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:mysql:${testcontainersVersion}"
}

test {
    useJUnitPlatform()
    jacoco {
        destinationFile = file(jacocoDestinationFile)
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

bootBuildImage {
    imageName = "beo1975/ronja-server:${versionMajor}.${versionMinor}.${versionPatch}"
}