plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.6'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '3.5.0.2730'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'org.openrewrite.rewrite' version '6.28.2'
}

group = 'com.ronja.crm'

def versionMajor = 1
def versionMinor = 3
def versionPatch = 2
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    set('sonarProjectName', 'RonjaServer') as Object
    set('sonarProjectKey', 'com.ronja.crm.ronjaserver') as Object
}

repositories {
    mavenCentral()
}

var bouncyCastleVersion = "1.70"
dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "com.mysql:mysql-connector-j"
    implementation "io.micrometer:micrometer-registry-prometheus:1.13.8"
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-mysql"
    implementation "org.yaml:snakeyaml:1.33"
    implementation 'org.apache.commons:commons-text:1.10.0'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:mockserver"
    testImplementation "org.mock-server:mockserver-client-java:5.15.0"
    testImplementation "org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}"
    testImplementation "org.bouncycastle:bcpkix-jdk15on:${bouncyCastleVersion}"
    testImplementation "io.rest-assured:spring-mock-mvc:5.3.0"
    testImplementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.10"
    testImplementation "com.squareup.okhttp3:mockwebserver:5.0.0-alpha.10"
    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:2.23.1"))
    rewrite("org.openrewrite.recipe:rewrite-migrate-java")
    rewrite("org.openrewrite.recipe:rewrite-spring")
}

rewrite {
    // https://docs.openrewrite.org/recipes/java/migrate
//    activeRecipe("org.openrewrite.java.migrate.UpgradeToJava17")
//    activeRecipe("org.openrewrite.java.migrate.util.ReplaceStreamCollectWithToList")
//    activeRecipe("org.openrewrite.java.spring.boot3.SpringBoot3BestPractices")
//    activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_3")
//    activeRecipe("org.openrewrite.java.RemoveUnusedImports")
//    activeRecipe("com.yourorg.NoStaticImportExample")
    activeRecipe("org.openrewrite.java.RemoveUnusedImports")
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.17.6"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

springBoot {
    buildInfo()
}

jacoco {
    toolVersion = '0.8.12'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_RonjaServer"
    }
}

bootJar {
    archiveFileName = "ronja-server.jar"
}

bootBuildImage {
    imageName = "beo1975/ronja-server:${versionMajor}.${versionMinor}.${versionPatch}"
}
