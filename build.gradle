plugins {
    id "java"
    id "org.springframework.boot" version "3.5.7"
    id "io.spring.dependency-management" version "1.1.7"
    id "jacoco"
    id "org.sonarqube" version "7.0.1.6134"
    id "com.gorylenko.gradle-git-properties" version "2.5.3"
    id "org.openrewrite.rewrite" version "7.20.0"
}

group = "com.ronja.crm"

def versionMajor = 1
def versionMinor = 3
def versionPatch = 2
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

ext {
    set("sonarProjectName", "RonjaServer") as Object
    set("sonarProjectKey", "com.ronja.crm.ronjaserver") as Object
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "com.mysql:mysql-connector-j"
    implementation "io.micrometer:micrometer-registry-prometheus:1.16.0"
    implementation "org.flywaydb:flyway-core"
    implementation "org.flywaydb:flyway-mysql"
    implementation "org.apache.commons:commons-text:1.14.0"

    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation(platform("org.testcontainers:testcontainers-bom:1.17.6"))
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql"
    testImplementation "org.testcontainers:mockserver"
    testImplementation "org.mock-server:mockserver-client-java:5.15.0"
    testImplementation "io.rest-assured:spring-mock-mvc:5.5.6"
    testImplementation "com.squareup.okhttp3:okhttp:5.3.0"
    testImplementation "com.squareup.okhttp3:mockwebserver:5.3.0"

    rewrite(platform("org.openrewrite.recipe:rewrite-recipe-bom:3.18.0"))
    rewrite("org.openrewrite.recipe:rewrite-migrate-java")
    rewrite("org.openrewrite.recipe:rewrite-spring")
}

rewrite {
//    activeRecipe("org.openrewrite.java.migrate.UpgradeToJava17")
    activeRecipe("org.openrewrite.java.migrate.UpgradeToJava25")
//    activeRecipe("org.openrewrite.java.migrate.util.ReplaceStreamCollectWithToList")
//    activeRecipe("org.openrewrite.java.spring.boot3.UpgradeSpringBoot_3_5")
    activeRecipe("org.openrewrite.java.ShortenFullyQualifiedTypeReferences")
    activeRecipe("com.ronja.crm.ronjaserver.ParametrizedRecipe")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

gitProperties {
    dotGitDirectory = project.rootProject.layout.projectDirectory.dir(".git")
}

springBoot {
    buildInfo()
}

jacoco {
    toolVersion = '0.8.14'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_RonjaServer"
    }
}

bootJar {
    archiveFileName = "ronja-server.jar"
}

bootBuildImage {
    imageName = "beo1975/ronja-server:${versionMajor}.${versionMinor}.${versionPatch}"
}
