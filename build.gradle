plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = 'com.ronja.crm'

def versionMajor = 0
def versionMinor = 6
def versionPatch = 0
version = "R${versionMajor}.${versionMinor}.${versionPatch}" as Object

ext {
    prometheusVersion = '1.7.5'
    flywayVersion = '8.0.2'
    testcontainersVersion = '1.16.2'
    jacocoDestinationFile = file("$buildDir/jacoco/test.exec")
    sonarProjectName = 'RonjaServer'
    sonarProjectKey = 'com.ronja.crm.ronjaserver'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("mysql:mysql-connector-java")
    implementation("io.micrometer:micrometer-registry-prometheus:${prometheusVersion}")
    implementation("org.flywaydb:flyway-core:${flywayVersion}")
    implementation platform("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
    testImplementation("org.testcontainers:junit-jupiter")
    testImplementation("org.testcontainers:mysql")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

test {
    useJUnitPlatform()
    jacoco {
        destinationFile = file(jacocoDestinationFile)
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

springBoot {
    buildInfo()
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "BranislavBeno_RonjaServer"
    }
}

bootJar {
    archiveFileName = "ronja-server.jar"
}

bootBuildImage {
    imageName = "beo1975/ronja-server:${versionMajor}.${versionMinor}.${versionPatch}"
}
